#!/usr/bin/env gura
import(re)
import(wx)
import(wx.utils)
import(jpeg)
import(translate)
import(global, g)
import(utils)

//-----------------------------------------------------------------------------
// Control ID
//-----------------------------------------------------------------------------
[
	ID_EditPattern
] = wx.ID_HIGHEST..

[
	TimerId_UpdateSample
	TimerId_DelayedUpdate
	TimerId_PollSourceDir
] = 1..

//-----------------------------------------------------------------------------
// PatternEditorPanel
//-----------------------------------------------------------------------------
PatternEditorPanel = class(wx.Panel) {
	dtSample = datetime(2015, 3, 21, 13, 24, 47)
	__init__(parent:wx.Windowm) = {|parent|
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.ALL, 2))
		wx.StaticBox(this, wx.ID_ANY, 'Substitution Pattern'$) {|stb|
			vboxIn = wx.StaticBoxSizer(stb, wx.VERTICAL)
			vbox.Add(vboxIn, wx.SizerFlags().Expand())
			gbox = wx.FlexGridSizer(nil, 2, 4, 8)
			vboxIn.Add(gbox, wx.SizerFlags().Expand())
			gbox.AddGrowableCol(1, 1)
			wx.StaticText(this, wx.ID_ANY, 'Folder Name'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Expand())
			}
			wx.TextCtrl(this, wx.ID_ANY, g.substPatternFolder) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags(1).Expand())
				ctrl.Bind(wx.EVT_TEXT) {|event| this.OnText(event)}
				this.textSubstPatternFolder = ctrl
			}
			wx.StaticText(this, wx.ID_ANY, 'File Name'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Expand())
			}
			wx.TextCtrl(this, wx.ID_ANY, g.substPatternFile) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags(1).Expand())
				ctrl.Bind(wx.EVT_TEXT) {|event| this.OnText(event)}
				this.textSubstPatternFile = ctrl
			}
		}
		str = format('Sample: '$ + '%04d-%02d-%02d %02d:%02d:%02d',
					 dtSample.year, dtSample.month, dtSample.day,
					 dtSample.hour, dtSample.min, dtSample.sec)
		wx.StaticBox(this, wx.ID_ANY, str) {|stb|
			vboxIn = wx.StaticBoxSizer(stb, wx.VERTICAL)
			vbox.Add(vboxIn, wx.SizerFlags().Expand().Border(wx.TOP, 8))
			gbox = wx.FlexGridSizer(nil, 2, 4, 8)
			vboxIn.Add(gbox, wx.SizerFlags().Expand())
			gbox.AddGrowableCol(1, 1)
			wx.StaticText(this, wx.ID_ANY, 'Folder Name'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Expand())
			}
			wx.TextCtrl(this, wx.ID_ANY, '', style => wx.TE_READONLY) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags(1).Expand())
				this.textResultFolder = ctrl
			}
			wx.StaticText(this, wx.ID_ANY, 'File Name'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Expand())
			}
			wx.TextCtrl(this, wx.ID_ANY, '', style => wx.TE_READONLY) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags(1).Expand())
				this.textResultFile = ctrl
			}
		}
		wx.HtmlWindow(this, style => wx.SUNKEN_BORDER) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags(1).Expand().Border(wx.TOP, 8))
			ctrl.SetPage('@PatternHelp'$)
		}
		wx.Button(this, wx.ID_OK, 'OK'$) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Border(wx.TOP, 8).Align(wx.ALIGN_CENTRE))
		}
		this.timer = wx.Timer(this, TimerId_UpdateSample)
		this.Bind(wx.EVT_TIMER, TimerId_UpdateSample) {|event| this.OnTimer(event)}
		this.UpdateSample()
	}
	OnText(event:wx.CommandEvent) = {
		this.timer.Start(500, true)
	}
	OnTimer(event:wx.TimerEvent) = {
		this.UpdateSample()
	}
	UpdateSample() = {
		substPatternFolder = this.textSubstPatternFolder.GetValue()
		substPatternFile = this.textSubstPatternFile.GetValue()
		[dirName, dirDispName, fileName] = \
			utils.ComposePathNameFromDatetime(substPatternFolder, substPatternFile, dtSample)
		this.textResultFolder.SetValue(dirDispName)
		this.textResultFile.SetValue(fileName)
	}
}

//-----------------------------------------------------------------------------
// PatternEditorDialog
//-----------------------------------------------------------------------------
PatternEditorDialog = class(wx.Dialog) {
	__init__(parent:wx.Window:nil, pos:wx.Point => wx.DefaultPosition) = \
		{|parent, wx.ID_ANY, 'Edit Pattern'$, pos, wx.Size(600, 500),
		  style => wx.DEFAULT_DIALOG_STYLE|
		this.SetIcon(g.icon)
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.ALL, 2))
		PatternEditorPanel(this) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags(1).Expand())
		}
	}
}

//-----------------------------------------------------------------------------
// ImageInfo
//-----------------------------------------------------------------------------
ImageInfo = struct(pathName:string, pathNameSource:string:nil, image:wx.Bitmap:nil,
				   stat:symbol, selectedFlag:boolean):map {
	UpdateImage(sizeThumbnail:number) = {
		try {
			this.image:wx.Bitmap = image(`rgb).read@jpeg(this.pathName, sizeThumbnail):fast:rough
			return(true)
		} catch {}
		false
	}
}

//-----------------------------------------------------------------------------
// ContainerInfo
//-----------------------------------------------------------------------------
ContainerInfo = struct(type:symbol, dirName:string, dirDispName:string,
					   imageInfos[]:ImageInfo, y:number, height:number,
					   rcBar:wx.Rect, rcExpand:wx.Rect, rcName:wx.Rect,
					   expandFlag:boolean)

//-----------------------------------------------------------------------------
// ImageViewer
//-----------------------------------------------------------------------------
ImageViewer = class(wx.Panel) {
	sizeThumbnail = 180
	wdSkip = 8, htSkip = 8
	htBar = 20
	htLabelMgn = 4
	htLabel = 16
	wdMgnLeft = 20, wdMgnRight = 4
	htMgnTop = 4, htMgnBottom = 8
	htScrollLine = 100
	htScrollPage = 400
	fileNamePatternAcceptable = ['*.JPG', '*.jpg', '*.JPEG', '*.jpeg']
	__init__(parent:wx.Windowm, style:number) \
		= {|parent, style => (style | wx.HSCROLL | wx.VSCROLL | wx.WANTS_CHARS)|
		this.bmpScreen = nil
		this.penSlotBlank = wx.TRANSPARENT_PEN
		this.brushSlotBlank = wx.Brush(wx.Colour(200, 200, 200), wx.BRUSHSTYLE_BDIAGONAL_HATCH)
		this.brushSlotBlank@Focused = wx.Brush(wx.Colour(192, 192, 192), wx.BRUSHSTYLE_SOLID)
		this.brushSlotBlank@Selected = wx.Brush(wx.Colour(192, 192, 192), wx.BRUSHSTYLE_SOLID)
		this.brushBg = wx.Brush(wx.WHITE, wx.BRUSHSTYLE_SOLID)
		this.penSlot = wx.TRANSPARENT_PEN
		this.brushSlot = wx.Brush(wx.Colour(200, 200, 200), wx.BRUSHSTYLE_SOLID)
		this.brushSlot@Focused = wx.Brush(wx.Colour(128, 128, 128), wx.BRUSHSTYLE_SOLID)
		this.brushSlot@Selected = wx.Brush(wx.Colour(128, 128, 128), wx.BRUSHSTYLE_SOLID)
		this.penSlotProgress = wx.TRANSPARENT_PEN
		this.brushSlotProgress = wx.Brush(wx.Colour(240, 240, 240), wx.BRUSHSTYLE_SOLID)
		this.fgBarText = wx.BLACK
		this.fgBarText@Focused = wx.WHITE
		this.fgBarLinkText = wx.Colour(0, 0, 255)
		this.fgBarLinkText@Focused = wx.WHITE
		this.fgLabelText = wx.BLACK
		this.fgLabelText@Focused = wx.WHITE
		this.fgLabelText@Selected = wx.WHITE
		this.brushBarSource = wx.Brush(wx.Colour(248, 234, 195), wx.BRUSHSTYLE_SOLID)
		this.brushBarStorage = wx.Brush(wx.Colour(248, 255, 248), wx.BRUSHSTYLE_SOLID)
		this.brushBarSource@Focused = wx.Brush(wx.Colour(0, 0, 200), wx.BRUSHSTYLE_SOLID)
		this.brushBarStorage@Focused = wx.Brush(wx.Colour(0, 0, 200), wx.BRUSHSTYLE_SOLID)
		this.brushLabel = wx.NullBrush
		this.brushLabel@Focused = wx.Brush(wx.Colour(0, 0, 200), wx.BRUSHSTYLE_SOLID)
		this.brushLabel@Selected = wx.Brush(wx.Colour(0, 0, 200), wx.BRUSHSTYLE_SOLID)
		this.containerInfosForSource = []
		this.containerInfosForStorage = []
		this.timer = wx.Timer(this, TimerId_DelayedUpdate)
		this.imageInfoQue = []
		this.containerInfoFocused = nil
		this.imageInfoFocused = nil
		this.font = wx.Font(11, wx.FONTFAMILY_DEFAULT, wx.FONTSTYLE_NORMAL,
							wx.FONTWEIGHT_NORMAL)
		this.fontBold = wx.Font(11, wx.FONTFAMILY_DEFAULT, wx.FONTSTYLE_NORMAL,
							wx.FONTWEIGHT_BOLD)
		this.fontBoldUL = wx.Font(11, wx.FONTFAMILY_DEFAULT, wx.FONTSTYLE_NORMAL,
							  wx.FONTWEIGHT_BOLD)
		this.fontBoldUL.SetUnderlined(true)
		this.nCols = 1
		this.Bind(wx.EVT_ERASE_BACKGROUND) { /* nothing to do */ }
		this.Bind(wx.EVT_SIZE) {|event| this.OnSize(event)}
		this.Bind(wx.EVT_TIMER, TimerId_DelayedUpdate) {|event| this.OnTimer(event)}
		this.Bind(wx.EVT_PAINT) {|event| this.OnPaint(event)}
		this.Bind(wx.EVT_SCROLLWIN) {|event| this.OnScrollWin(event)}
		this.Bind(wx.EVT_MOUSEWHEEL) {|event| this.OnMouseWheel(event)}
		this.Bind(wx.EVT_LEFT_DOWN) {|event| this.OnLeftDown(event)}
		this.Bind(wx.EVT_LEFT_UP) {|event| this.OnLeftUp(event)}
		this.Bind(wx.EVT_MOTION) {|event| this.OnMotion(event)}
		this.Bind(wx.EVT_KEY_DOWN) {|event| this.OnKeyDown(event)}
		this.Bind(wx.EVT_IDLE) {|event| this.OnIdle(event)}
	}
	InitSelection() = {
		this.containerInfoFocused = nil
		this.imageInfoFocused = nil
		if (!this.containerInfosForSource.isempty()) {
			this.containerInfoFocused = this.containerInfosForSource.first()
		} elsif (!this.containerInfosForStorage.isempty()) {
			this.containerInfoFocused = this.containerInfosForStorage.first()
		}
	}
	UpdateContainerInfosForSource() = {
		pathNamesSource = path.walk(g.dirNameSource, -1, fileNamePatternAcceptable*):file
		this.containerInfosForSource = []
		pathNamesSource.each():xlist {|pathNameSource|
			dt = utils.GetTimeStamp(pathNameSource)
			!dt && continue
			[dirName, dirDispName, fileName] = utils.ComposePathNameFromDatetime(
					g.substPatternFolder, g.substPatternFile, dt)
			if (idx = (this.containerInfosForSource:*dirName == dirName).find():index) {
				containerInfo = this.containerInfosForSource[idx]
			} else {
				containerInfo = ContainerInfo(
						`source, dirName, dirDispName, [], 0, 0,
						wx.Rect(0, 0, 0, 20), wx.Rect(0, 0, 20, 20), wx.Rect(0, 0, 0, 20),
						false)
				this.containerInfosForSource.add(containerInfo)
			}
			containerInfo.imageInfos.add(ImageInfo(
					path.join(dirName, fileName), pathNameSource, nil, `source, false))
		}
		!this.containerInfosForSource.isempty()
	}
	UpdateContainerInfosForStorage() = {
		matchPatternFolder = utils.MakeMatchPatternFolder(g.substPatternFolder)
		dirNames = (path.walk(g.dirNameStorage, 1, '*'):dir).sort(`descend):list
		this.containerInfosForStorage = dirNames.each():xlist {|dirName|
			pathNames = [path.dir(dirName, fileNamePatternAcceptable*)]
			pathNames.isempty() && continue
			dirName = dirName.chop()
			dirDispName = dirName.mid(g.dirNameStorage.len() + 1)
			!dirDispName.match(matchPatternFolder) && continue
			ContainerInfo(
					`storage, dirName, dirDispName, ImageInfo(pathNames, nil, nil, `storage, false),
					0, 0,
					wx.Rect(0, 0, 0, 20), wx.Rect(0, 0, 20, 20), wx.Rect(0, 0, 0, 20),
					false)
		}
		!this.containerInfosForStorage.isempty()
	}
	EnsureSelectionVisible() = {
		[wdClient, htClient] = this.GetClientSizeWH()
		yScrollPos = this.GetScrollPos(wx.VERTICAL)
		if (this.imageInfoFocused) {
			idxImage = this.containerInfoFocused.imageInfos.find(this.imageInfoFocused):index
			[xSlot, ySlot] = this.CalcSlotPosition(this.containerInfoFocused, idxImage)
			yTop = ySlot
			yBottom = yTop + sizeThumbnail + htLabelMgn + htLabel
		} else {
			yTop = this.containerInfoFocused.y
			yBottom = yTop + htBar
		}
		if (yTop < yScrollPos) {
			this.SetScrollPos(wx.VERTICAL, yTop)
		} elsif (yBottom > yScrollPos + htClient) {
			this.SetScrollPos(wx.VERTICAL, yBottom - htClient)
		}
	}
	UpdateContent() = {
		[wdClient, htClient] = this.GetClientSizeWH()
		y = 0
		this.nCols = int((wdClient - (wdMgnLeft + wdMgnRight) + wdSkip) / (sizeThumbnail + wdSkip))
		containerInfos = (
				this.containerInfosForSource.each(),
				this.containerInfosForStorage.each())
		containerInfos.each {|containerInfo|
			containerInfo.y = y
			containerInfo.rcBar.y = y
			containerInfo.rcBar.width = wdClient
			containerInfo.rcExpand.y = y
			containerInfo.rcName.y = y
			height = 0
			height += htBar
			if (containerInfo.expandFlag) {
				height += htMgnTop
				nRows = int((containerInfo.imageInfos.len() + this.nCols - 1) / this.nCols)
				htImages = nRows * (sizeThumbnail + htLabelMgn + htLabel)
				if (nRows > 1) { htImages += (nRows - 1) * htSkip }
				height += htImages
			}
			height += htMgnBottom
			containerInfo.height = height
			y += height
		}
		htRequired = y
		htScreen = max(htClient, htRequired)
		this.bmpScreen = wx.BitmapWH(wdClient, htClient)
		dc = wx.MemoryDC(this.bmpScreen)
		dc.SetBackground(this.brushBg)
		dc.Clear()
		yScrollPos = this.GetScrollPos(wx.VERTICAL)
		dc.SetDeviceOrigin(0, -yScrollPos)
		matchPatternFolder = utils.MakeMatchPatternFolder(g.substPatternFolder)
		containerInfos.each {|containerInfo|
			containerInfo.rcsSplit = []
			(containerInfo.y + containerInfo.height < yScrollPos) && continue
			(containerInfo.y >= yScrollPos + htClient) && break
			htDisp = containerInfo.y + containerInfo.height - yScrollPos
			if (containerInfo.expandFlag) {
				containerInfo.imageInfos.each {|imageInfo, idxImage|
					focusedFlag = imageInfo.is(this.imageInfoFocused)
					[xSlot, ySlot] = this.CalcSlotPosition(containerInfo, idxImage)
					(ySlot + sizeThumbnail <= yScrollPos) && continue
					(ySlot >= yScrollPos + htClient) && break
					iCol = idxImage % this.nCols
					if (idxImage == 0) {
						// nothing to do
					} elsif (iCol == 0) {
						rc = wx.Rect(0, ySlot - htSkip, wdClient, htSkip)
					} else {
						rc = wx.Rect(xSlot - wdSkip, ySlot, wdSkip, sizeThumbnail)
					}
					if (imageInfo.stat == `source) {
						dc.SetPen(this.penSlotBlank)
						if (focusedFlag) {
							dc.SetBrush(this.brushSlotBlank@Focused)
						} elsif (imageInfo.selectedFlag) {
							dc.SetBrush(this.brushSlotBlank@Selected)
						} else {
							dc.SetBrush(this.brushSlotBlank)
						}
						dc.DrawRectangle(xSlot, ySlot, sizeThumbnail, sizeThumbnail)
						str = path.filename(imageInfo.pathNameSource)
						dc.SetFont(this.font)
						[wdStr, htStr] = dc.GetTextExtent(str)
						xStr = xSlot + (sizeThumbnail - wdStr) / 2
						yStr = ySlot + (sizeThumbnail - htStr) / 2
						dc.SetTextForeground(wx.Colour(140, 140, 140))
						dc.DrawText(str, xStr, yStr)
					} elsif (imageInfo.stat == `storage) {
						dc.SetPen(this.penSlot)
						if (focusedFlag) {
							dc.SetBrush(this.brushSlot@Focused)
						} elsif (imageInfo.selectedFlag) {
							dc.SetBrush(this.brushSlot@Selected)
						} else {
							dc.SetBrush(this.brushSlot)
						}
						dc.DrawRectangle(xSlot, ySlot, sizeThumbnail, sizeThumbnail)
						if (imageInfo.image) {
							xImage = xSlot + (sizeThumbnail - imageInfo.image.width) / 2
							yImage = ySlot + (sizeThumbnail - imageInfo.image.height) / 2
							dc.DrawBitmap(imageInfo.image, xImage, yImage, false)
						}
					} elsif (imageInfo.stat == `progress) {
						dc.SetPen(this.penSlotProgress)
						dc.SetBrush(this.brushSlotProgress)
						dc.DrawRectangle(xSlot, ySlot, sizeThumbnail, sizeThumbnail)
					}
					str = path.filename(imageInfo.pathName)
					[wdStr, htStr] = dc.GetTextExtent(str)
					xStr = xSlot + (sizeThumbnail - wdStr) / 2
					yStr = ySlot + sizeThumbnail + htLabelMgn + (htLabel - htStr) / 2
					dc.SetPen(wx.TRANSPARENT_PEN)
					dc.SetFont(this.font)
					if (focusedFlag) {
						dc.SetBrush(this.brushLabel@Focused)
						dc.SetTextForeground(this.fgLabelText@Focused)
					} elsif (imageInfo.selectedFlag) {
						dc.SetBrush(this.brushLabel@Selected)
						dc.SetTextForeground(this.fgLabelText@Selected)
					} else {
						dc.SetBrush(this.brushLabel)
						dc.SetTextForeground(this.fgLabelText)
					}
					dc.DrawRectangle(xStr, yStr, wdStr, htLabel)
					dc.DrawText(str, xStr, yStr)
				}
			}
			if (htDisp > htBar) {
				focusedFlag = containerInfo.is(this.containerInfoFocused) && !this.imageInfoFocused
				yBar = max(yScrollPos, containerInfo.y)
				containerInfo.rcBar.y = yBar
				containerInfo.rcExpand.y = yBar
				containerInfo.rcName.y = yBar
				dc.SetPen(wx.TRANSPARENT_PEN)
				if (focusedFlag) {
					dc.SetBrush(cond(containerInfo.type == `source,
									 this.brushBarSource@Focused, this.brushBarStorage@Focused))
				} else {
					dc.SetBrush(cond(containerInfo.type == `source,
									 this.brushBarSource, this.brushBarStorage))
				}
				dc.DrawRectangle(0, yBar, wdClient, htBar)
				if (containerInfo.expandFlag) {
					dc.DrawBitmap(g.bmpExpandMinus, 0, yBar, false)
				} else {
					dc.DrawBitmap(g.bmpExpandPlus, 0, yBar, false)
				}
				if (m = containerInfo.dirDispName.match(matchPatternFolder)) {
					strLeft = m[0]
					strRight = containerInfo.dirDispName.mid(strLeft.len())
					if (strRight.match('^#')) {
						strRight = 'Click to Fill Name'$
					}
					dc.SetFont(this.fontBold)
					dc.SetTextForeground(cond(focusedFlag,
											  this.fgBarText@Focused, this.fgBarText))
					dc.DrawText(strLeft, 20, yBar)
					[wdStr, htStr] = dc.GetTextExtent(strLeft)
					dc.SetFont(this.fontBoldUL)
					dc.SetTextForeground(cond(focusedFlag,
											  this.fgBarLinkText@Focused, this.fgBarLinkText))
					dc.DrawText(strRight, 20 + wdStr, yBar)
				} else {
					dc.SetFont(this.fontBold)
					dc.SetTextForeground(cond(focusedFlag,
											  this.fgBarText@Focused, this.fgBarText))
					dc.DrawText(containerInfo.dirDispName, 20, yBar)
				}
				str = format('%d images'$, containerInfo.imageInfos.len())
				[wdStr, htStr] = dc.GetTextExtent(str)
				dc.SetFont(this.fontBold)
				dc.SetTextForeground(cond(focusedFlag,
										  this.fgBarText@Focused, this.fgBarText))
				dc.DrawText(str, wdClient - wdStr, yBar)
			}
		}
		dc = nil
		this.SetScrollbar(wx.VERTICAL, this.GetScrollPos(wx.VERTICAL), htClient, htScreen)
		this.Refresh()
		this.Update()
	}
	CalcSlotPosition(containerInfo:ContainerInfo, idxImage:number) = {
		xSlot = wdMgnLeft + (idxImage % this.nCols) * (sizeThumbnail + wdSkip)
		ySlot = containerInfo.y + htBar + htMgnTop + \
			int(idxImage / this.nCols) * (sizeThumbnail + htLabelMgn + htLabel + htSkip)
		[xSlot, ySlot]
	}
	CopyImageFiles(label:wx.StaticText) = {
		[wdClient, htClient] = this.GetClientSizeWH()
		this.quitFlag = false
		nImages = this.containerInfosForSource:*imageInfos:*len().sum()
		idxImageWhole = 0
		htSlot = sizeThumbnail + htLabelMgn + htLabel
		yScrollPos = this.GetScrollPos(wx.VERTICAL)
		containerInfo = this.containerInfosForSource.first()
		if (containerInfo.y < yScrollPos) {
			this.SetScrollPos(wx.VERTICAL, containerInfo.y)
			this.UpdateContent()
		}
		nImagesSuccess = 0
		nImagesFailed = 0
		this.containerInfosForSource.each {|containerInfo|
			if (!containerInfo.expandFlag) {
				containerInfo.expandFlag = true
				this.UpdateContent()
			}
			containerInfo.imageInfos.each {|imageInfo, idxImageInfo|
				label.SetLabel(format('%d/%d', idxImageWhole + 1, nImages))
				dirName = path.dirname(imageInfo.pathName)
				!path.exists(dirName) && fs.mkdir(dirName):tree
				[xSlot, ySlot] = this.CalcSlotPosition(containerInfo, idxImageInfo)
				yScrollPos = this.GetScrollPos(wx.VERTICAL)
				if (ySlot + htSlot > yScrollPos + htClient) {
					yScrollPos = ySlot + htSlot - htClient
					this.SetScrollPos(wx.VERTICAL, yScrollPos)
					this.UpdateContent()
				}
				imageInfo.stat = `progress
				this.UpdateContent()
				stat = path.stat(imageInfo.pathNameSource)
				successFlag = false
				try {
					stream.copy(imageInfo.pathNameSource, imageInfo.pathName):finalize {
						this.Update()
						wx.App.GetInstance().Yield()
						this.quitFlag && break
					}
					if (imageInfo.UpdateImage(sizeThumbnail)) {
						imageInfo.stat = `storage
						this.UpdateContent()
						successFlag = true
					} else {
						imageInfo.stat = `source
					}
				} catch {}
				if (successFlag) {
					nImagesSuccess += 1
				} else {
					nImagesFailed += 1
				}
				this.quitFlag && break
				idxImageWhole += 1
			}
			this.quitFlag && break
		}
		[nImagesSuccess, nImagesFailed]
	}
	QuitCopy() = {
		this.quitFlag = true
	}
	ChangeExpandStatus(containerInfo:ContainerInfo) = {
		if (containerInfo.type == `storage) {
			if (containerInfo.expandFlag) {
				this.imageInfoQue.append(containerInfo.imageInfos)
			} else {
				indices = this.imageInfoQue.find(containerInfo.imageInfos):map:index:xlist
				this.imageInfoQue.erase(indices*)
			}
		}
	}
	SenseBox(pt:wx.Position, clickFlag:boolean) = {
		x = pt.x
		y = pt.y + this.GetScrollPos(wx.VERTICAL)
		[this.containerInfosForSource, this.containerInfosForStorage].each {|containerInfos|
			if (idx = containerInfos:*rcExpand:*ContainsXY(x, y).find():index) {
				if (clickFlag) {
					containerInfo = containerInfos[idx]
					containerInfo.expandFlag = !containerInfo.expandFlag
					this.ChangeExpandStatus(containerInfo)
					this.UpdateContent()
				}
				this.SetCursor(wx.StockCursor(wx.CURSOR_HAND))
				return
			}
			if (idx = containerInfos:*rcBar:*ContainsXY(x, y).find():index) {
				if (clickFlag) {
					containerInfo = containerInfos[idx]
					this.containerInfoFocused = containerInfo
					this.imageInfoFocused = nil
					this.UpdateContent()
				}
				this.SetCursor(wx.StockCursor(wx.CURSOR_HAND))
				return
			}
			containerInfos.each {|containerInfo|
				
			}
		}
		this.SetCursor(wx.StockCursor(wx.CURSOR_ARROW))
	}
	OnLeftDown(event:wx.MouseEvent) = {
		this.SetFocus()
		this.SenseBox(event.GetPosition(), true)
	}
	OnLeftUp(event:wx.MouseEvent) = {
	}
	OnMotion(event:wx.MouseEvent) = {
		this.SenseBox(event.GetPosition(), false)
	}
	OnSize(event:wx.SizeEvent) = {
		this.timer.Start(100, true)
		event.Skip()
	}
	OnTimer(event:wx.TimerEvent) = {
		this.UpdateContent()
	}
	OnPaint(event:wx.PaintEvent) = {
		dc = wx.PaintDC(this)
		if (this.bmpScreen) {
			x = this.GetScrollPos(wx.HORIZONTAL)
			dc.DrawBitmap(this.bmpScreen, -x, 0, false)
		}
		dc = nil
	}
	OnScrollWin(event:wx.ScrollWinEvent) = {
		eventType = event.GetEventType()
		orientation = event.GetOrientation()
		if (eventType == wx.EVT_SCROLLWIN_TOP.GetEventType()) {
			pos = 0
		} elsif (eventType == wx.EVT_SCROLLWIN_BOTTOM.GetEventType()) {
			pos = this.GetScrollRange(orientation)
		} elsif (eventType == wx.EVT_SCROLLWIN_LINEUP.GetEventType()) {
			pos = this.GetScrollPos(orientation) - htScrollLine
		} elsif (eventType == wx.EVT_SCROLLWIN_LINEDOWN.GetEventType()) {
			pos = this.GetScrollPos(orientation) + htScrollLine
		} elsif (eventType == wx.EVT_SCROLLWIN_PAGEUP.GetEventType()) {
			pos = this.GetScrollPos(orientation) - htScrollPage
		} elsif (eventType == wx.EVT_SCROLLWIN_PAGEDOWN.GetEventType()) {
			pos = this.GetScrollPos(orientation) + htScrollPage
		} elsif (eventType == wx.EVT_SCROLLWIN_THUMBTRACK.GetEventType()) {
			pos = event.GetPosition()
		} elsif (eventType == wx.EVT_SCROLLWIN_THUMBRELEASE.GetEventType()) {
			pos = event.GetPosition()
		}
		this.SetScrollPos(orientation, pos)
		this.UpdateContent()
	}
	OnMouseWheel(event:wx.MouseEvent) = {
		orientation = wx.VERTICAL
		rot = event.GetWheelRotation()
		pos = this.GetScrollPos(orientation)
		if (rot < 0) {
			this.SetScrollPos(orientation, pos + htScrollLine)
			this.UpdateContent()
		} elsif (rot > 0) {
			this.SetScrollPos(orientation, pos - htScrollLine)
			this.UpdateContent()
		}
	}
	OnKeyDown(event:wx.KeyEvent) = {
		shiftFlag = event.ShiftDown()
		keyCode = event.GetKeyCode()
		if (keyCode == wx.WXK_UP) {
			if (this.imageInfoFocused) {
				if (!shiftFlag) {
					this.containerInfoFocused.imageInfos::selectedFlag = false
				}
				imageInfos = this.containerInfoFocused.imageInfos
				idxImagePrev = imageInfos.find(this.imageInfoFocused):index
				idxImage = idxImagePrev - this.nCols
				if (idxImage >= 0) {
					if (shiftFlag) {
						this.imageInfoFocused.selectedFlag = true
					}
					this.imageInfoFocused = imageInfos[idxImage]
				} else {
					this.imageInfoFocused = nil
				}
			} else {
				selectImageFlag = false
				idxContainer = this.containerInfosForSource.find(this.containerInfoFocused):index
				if (idxContainer) {
					if (idxContainer > 0) {
						this.containerInfoFocused = this.containerInfosForSource[idxContainer - 1]
						selectImageFlag = true
					}
				} else {
					idxContainer = this.containerInfosForStorage.find(this.containerInfoFocused):index
					if (idxContainer > 0) {
						this.containerInfoFocused = this.containerInfosForStorage[idxContainer - 1]
						selectImageFlag = true
					} elsif (!this.containerInfosForSource.isempty()) {
						this.containerInfoFocused = this.containerInfosForSource.last()
						selectImageFlag = true
					}
				}
				if (selectImageFlag) {
					imageInfos = this.containerInfoFocused.imageInfos
					if (!imageInfos.isempty() && this.containerInfoFocused.expandFlag) {
						idxImage = int((imageInfos.len() - 1) / this.nCols) * this.nCols
						this.imageInfoFocused = imageInfos[idxImage]
					}
				}
			}
			this.EnsureSelectionVisible()
			this.UpdateContent()
		} elsif (keyCode == wx.WXK_DOWN) {
			forwardContainerFlag = false
			imageInfos = this.containerInfoFocused.imageInfos
			if (this.imageInfoFocused) {
				if (!shiftFlag) {
					this.containerInfoFocused.imageInfos::selectedFlag = false
				}
				idxImagePrev = imageInfos.find(this.imageInfoFocused):index
				idxImage = idxImagePrev + this.nCols
				if (idxImage < imageInfos.len()) {
					if (shiftFlag) {
						imageInfos[idxImagePrev .. idxImage]::selectedFlag = true
					}
					this.imageInfoFocused = imageInfos[idxImage]
				} else {
					forwardContainerFlag = true
					this.imageInfoFocused = nil
				}
			} elsif (imageInfos.isempty() || !this.containerInfoFocused.expandFlag) {
				forwardContainerFlag = true
			} else {
				this.imageInfoFocused = imageInfos.first()
			}
			if (forwardContainerFlag) {
				idxContainer = this.containerInfosForSource.find(this.containerInfoFocused):index
				if (idxContainer) {
					if (idxContainer + 1 < this.containerInfosForSource.len()) {
						this.containerInfoFocused = this.containerInfosForSource[idxContainer + 1]
					} elsif (!this.containerInfosForStorage.isempty()) {
						this.containerInfoFocused = this.containerInfosForStorage.first()
					}
				} else {
					idxContainer = this.containerInfosForStorage.find(this.containerInfoFocused):index
					if (idxContainer + 1 < this.containerInfosForStorage.len()) {
						this.containerInfoFocused = this.containerInfosForStorage[idxContainer + 1]
					}
				}
			}
			this.EnsureSelectionVisible()
			this.UpdateContent()
		} elsif (keyCode == wx.WXK_LEFT) {
			imageInfos = this.containerInfoFocused.imageInfos
			if (this.imageInfoFocused) {
				if (!shiftFlag) {
					this.containerInfoFocused.imageInfos::selectedFlag = false
				}
				idxImage = imageInfos.find(this.imageInfoFocused):index
				if (idxImage > 0) {
					if (shiftFlag) {
						this.imageInfoFocused.selectedFlag = true
					}
					this.imageInfoFocused = imageInfos[idxImage - 1]
				}
			} else {
				this.containerInfoFocused.expandFlag = false
				this.ChangeExpandStatus(this.containerInfoFocused)
			}
			this.EnsureSelectionVisible()
			this.UpdateContent()
		} elsif (keyCode == wx.WXK_RIGHT) {
			imageInfos = this.containerInfoFocused.imageInfos
			if (this.imageInfoFocused) {
				if (!shiftFlag) {
					this.containerInfoFocused.imageInfos::selectedFlag = false
				}
				idxImage = imageInfos.find(this.imageInfoFocused):index
				if (idxImage + 1 < imageInfos.len()) {
					if (shiftFlag) {
						this.imageInfoFocused.selectedFlag = true
					}
					this.imageInfoFocused = imageInfos[idxImage + 1]
				}
			} else {
				this.containerInfoFocused.expandFlag = true
				this.ChangeExpandStatus(this.containerInfoFocused)
			}
			this.EnsureSelectionVisible()
			this.UpdateContent()
		} elsif (keyCode == wx.WXK_HOME) {
			imageInfos = this.containerInfoFocused.imageInfos
			if (this.imageInfoFocused) {
				this.imageInfoFocused = imageInfos[0]
			} else {
				// nothing to do
			}
			this.EnsureSelectionVisible()
			this.UpdateContent()
		} elsif (keyCode == wx.WXK_END) {
			imageInfos = this.containerInfoFocused.imageInfos
			if (this.imageInfoFocused) {
				this.imageInfoFocused = imageInfos[-1]
			} else {
				// nothing to do
			}
			this.EnsureSelectionVisible()
			this.UpdateContent()
		}
	}
	OnIdle(event:wx.IdleEvent) = {
		this.imageInfoQue.isempty() && return
		updateFlag = false
		repeat (5) {
			this.imageInfoQue.isempty() && break
			imageInfo = this.imageInfoQue[0]
			this.imageInfoQue.erase(0)
			if (imageInfo.UpdateImage(sizeThumbnail)) {
				updateFlag = true
			}
		}
		updateFlag && this.UpdateContent()
	}
}

//-----------------------------------------------------------------------------
// MainPanel
//-----------------------------------------------------------------------------
MainPanel = class(wx.Panel) {
	__init__(parent:wx.Windowm) = {|parent|
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.ALL, 4))
		wx.FlexGridSizer(nil, 3, 4, 4) {|gbox|
			vbox.Add(gbox, wx.SizerFlags().Expand())
			gbox.AddGrowableCol(1, 1)
			wx.StaticText(this, wx.ID_ANY, 'Source Folder'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
			}
			wx.ComboBox(this, wx.ID_ANY, style => wx.CB_READONLY) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags(1).Expand())
				this.Bind(wx.EVT_COMBOBOX, ctrl.GetId()) {|event|
					this.OnComboBox_Select(event)
				}
				this.cbSource = ctrl
			}
			wx.Button(this, wx.ID_ANY, 'Browse'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags())
				this.Bind(wx.EVT_BUTTON, ctrl.GetId()) {|event|
					this.OnBtn_SourceBrowse(event)
				}
			}
			
			wx.StaticText(this, wx.ID_ANY, 'Storage Folder'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
			}
			wx.StaticText(this, wx.ID_ANY, g.dirNameStorage,
						  style => wx.SUNKEN_BORDER | wx.ST_NO_AUTORESIZE) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags(1).Expand())
				this.textStorage = ctrl
			}
			wx.Button(this, wx.ID_ANY, 'Browse'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags())
				this.Bind(wx.EVT_BUTTON, ctrl.GetId()) {|event|
					this.OnBtn_StorageBrowse(event)
				}
			}
		}
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Align(wx.CENTRE).Border(wx.TOP, 4))
			wx.Button(this, wx.ID_ANY, 'Start to Copy Image Files'$,
					  size => wx.Size(300, -1)) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand())
				this.Bind(wx.EVT_BUTTON, ctrl.GetId()) {|event|
					this.OnBtn_CopyImageFiles(event)
				}
				this.btnStart = ctrl
			}
			wx.StaticText(this, wx.ID_ANY, '', size => wx.Size(100, -1),
						  style => wx.ST_NO_AUTORESIZE | wx.ALIGN_CENTRE_HORIZONTAL | \
						  			wx.BORDER_SUNKEN) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 8))
				this.labelIndicator = ctrl
			}
			wx.Button(this, wx.ID_ANY, 'Quit'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 8))
				this.Bind(wx.EVT_BUTTON, ctrl.GetId()) {|event|
					this.OnBtn_Quit(event)
				}
				this.btnQuit = ctrl
			}
		}
		ImageViewer(this, style => wx.SUNKEN_BORDER) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags(1).Expand().Border(wx.TOP, 4))
			this.imageViewer = ctrl
		}
		this.btnQuit.Enable(false)
		this.timer = wx.Timer(this, TimerId_PollSourceDir)
		this.btnStart.Enable(this.imageViewer.UpdateContainerInfosForSource())
		this.imageViewer.UpdateContainerInfosForStorage()
		this.imageViewer.InitSelection()
		this.dirNamesDCIM = []
		this.Bind(wx.EVT_TIMER, TimerId_PollSourceDir) {|event| this.OnTimer(event)}
		this.timer.Start(1000, false)
		this.UpdateSource()
	}
	ChangeSource() = {
		this.btnStart.Enable(this.imageViewer.UpdateContainerInfosForSource())
		this.imageViewer.InitSelection()
		this.imageViewer.UpdateContent()
	}
	OnComboBox_Select(event:wx.ComboBoxEvent) = {
		g.dirNameSource = this.cbSource.GetString(event.GetSelection())
		this.ChangeSource()
	}
	OnBtn_SourceBrowse(event:wx.CommandEvent) = {
		style = wx.DD_DEFAULT_STYLE | wx.DD_DIR_MUST_EXIST
		dlg = wx.DirDialog(this, 'Choose Source Folder'$, g.dirNameBrowsed, style)
		if (dlg.ShowModal() == wx.ID_OK) {
			dirName = dlg.GetPath()
			g.dirNameBrowsed = dirName
			g.dirNameSource = dirName
			this.UpdateSource()
		}
	}
	OnBtn_StorageBrowse(event:wx.CommandEvent) = {
		style = wx.DD_DEFAULT_STYLE | wx.DD_DIR_MUST_EXIST
		dlg = wx.DirDialog(this, 'Choose Storage Folder'$,
						   g.dirNameStorage, style)
		if (dlg.ShowModal() == wx.ID_OK) {
			dirName = dlg.GetPath()
			g.dirNameStorage = dirName
			this.textStorage.SetLabel(dirName)
			this.imageViewer.UpdateContainerInfosForStorage()
			this.imageViewer.InitSelection()
			this.imageViewer.UpdateContent()
		}
	}
	OnBtn_CopyImageFiles(event:wx.CommandEvent) = {
		this.btnStart.Enable(false)
		this.btnQuit.Enable(true)
		[nImagesSuccess, nImagesFailed] = this.imageViewer.CopyImageFiles(this.labelIndicator)
		this.btnStart.Enable(true)
		this.btnQuit.Enable(false)
		wx.MessageDialog(this,
						 format('%d images have been successfully copied.'$, nImagesSuccess),
						 'Copy Completed'$, wx.OK) {|dlg|
			dlg.CentreOnParent()
			dlg.ShowModal()
		}
	}
	OnBtn_Quit(event:wx.CommandEvent) = {
		this.imageViewer.QuitCopy()
	}
	OnTimer(event:wx.TimerEvent) = {
		dirNamesDCIM = utils.RetrieveDirNamesDCIM()
		(this.dirNamesDCIM.len() == dirNamesDCIM.len()) && \
			(this.dirNamesDCIM.isempty() || \
			 (this.dirNamesDCIM == dirNamesDCIM).and()) && return
		this.dirNamesDCIM = dirNamesDCIM
		this.UpdateSource()
	}
	UpdateSource() = {
		this.cbSource.Clear()
		this.cbSource.Append(this.dirNamesDCIM)
		if (!g.dirNameBrowsed.isempty()) {
			this.cbSource.Append(g.dirNameBrowsed)
		}
		if ((idx = this.cbSource.FindString(g.dirNameSource)) < 0) { idx = 0 }
		this.cbSource.SetSelection(idx)
		g.dirNameSource = this.cbSource.GetString(idx)
		this.ChangeSource()
	}
}

//-----------------------------------------------------------------------------
// MainFrame
//-----------------------------------------------------------------------------
MainFrame = class(wx.Frame) {
	__init__(pos:wx.Point => wx.DefaultPosition) = \
		{|nil, wx.ID_ANY, 'Gura Capture', wx.Point(g.xMainWindow, g.yMainWindow),
		  wx.Size(g.wdMainWindow, g.htMainWindow)|
		this.SetIcon(g.icon)
		menuBar = wx.MenuBar()
		MainPanel(this)
		this.SetMenuBar(menuBar)
		wx.Menu {|m|
			menuBar.Append(m, '&Tools'$)
			m.Append(ID_EditPattern,		'&Edit Pattern...\tCtrl-E'$)
		}								
		this.Bind(wx.EVT_MENU, ID_EditPattern)	{|event| this.OnMenu_EditPattern(event)}
		this.Bind(wx.EVT_SIZE) {|event|
			//wx.LayoutAlgorithm().LayoutWindow(this, this.productViewer)
			this.UpdateSizePosInfo()
			event.Skip()
		}
	}
	UpdateSizePosInfo() = {
		(this.IsIconized() || this.IsMaximized()) && return
		[g.xMainWindow, g.yMainWindow] = this.GetScreenPositionXY()
		[g.wdMainWindow, g.htMainWindow] = this.GetSizeWH()
	}
	OnMenu_EditPattern(event:wx.CommandEvent) = {
		dlg = PatternEditorDialog(this)
		dlg.ShowModal()
	}
}

//-----------------------------------------------------------------------------
// App
//-----------------------------------------------------------------------------
App = class(wx.App) {
	OnInit() = {
		!wx.utils.CheckGuraVersion(nil, 0, 7, 0) && return(false)
		!wx.utils.CheckGuraPlatform(nil, [`mswin, `linux, `darwin]) && return(false)
		g.InitializeResource()
		g.LoadConfig()
		frame = MainFrame()
		frame.Show()
		true
		//dlg = PatternEditorDialog(nil)
		//dlg.ShowModal()
		//false
	}
	OnExit() = {
		g.SaveConfig()
		0
	}
}

wx.IMPLEMENT_APP(App)
